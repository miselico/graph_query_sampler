SELECT DISTINCT ?s0 ?p0 (group_concat(distinct ?o0_o1_o2; separator="|") AS ?o0_o1_o2_targets)  ?s1 ?p1 ?s2 ?p2 ?diameter

WHERE {

	BIND (1 as ?diameter)
    graph <split:train> {
        ?s0 ?p0 ?o0_o1_o2 .
        ?s1 ?p1 ?o0_o1_o2 .
        ?s2 ?p2 ?o0_o1_o2 .
    }

    #Make sure all 3 incoming edges are different, to avoid degrading to 2i or one hop
    FILTER(!(?s0=?s1 && ?p0=?p1))
    FILTER(!(?s0=?s2 && ?p0=?p2))
    FILTER(!(?s1=?s2 && ?p1=?p2))

    #getting rid of permutations of the incoming edges
    FILTER(str(?s0) < str(?s1) || (str(?s0)=str(?s1) && str(?p0) < str(?p1)))
    FILTER(str(?s1) < str(?s2) || (str(?s1)=str(?s2) && str(?p1) < str(?p2)))

    FILTER(isIRI(?s0) && isIRI(?s1) && isIRI(?s2) && isIRI(?o0_o1_o2))



### restrictions ###




} GROUP BY ?s0 ?p0 ?s1 ?p1 ?s2 ?p2 ?diameter